// Joining dialogue
IF
Global("O#TiaxSpawn","GLOBAL",1)
THEN
RESPONSE #100
StartDialogueNoSet([PC])
Wait(1)
END

// Experience fixer
// 800000
IF
InParty(Myself)
Global("O#TiaxExperienceSet","GLOBAL",0)
XPGT(Player1,600000)
XPLT(Player1,1000001)
THEN
RESPONSE #100
SetGlobal("O#TiaxExperienceSet","GLOBAL",1)
AddXPObject("O#Tiax",400000)
END

// 1200000
IF
InParty(Myself)
Global("O#TiaxExperienceSet","GLOBAL",0)
XPGT(Player1,1000000)
XPLT(Player1,1400001)
THEN
RESPONSE #100
SetGlobal("O#TiaxExperienceSet","GLOBAL",1)
AddXPObject("O#Tiax",800000)
END

// 1600000
IF
InParty(Myself)
Global("O#TiaxExperienceSet","GLOBAL",0)
XPGT(Player1,1400000)
XPLT(Player1,1800001)
THEN
RESPONSE #100
SetGlobal("O#TiaxExperienceSet","GLOBAL",1)
AddXPObject("O#Tiax",1200000)
END

// 2000000
IF
InParty(Myself)
Global("O#TiaxExperienceSet","GLOBAL",0)
XPGT(Player1,1800000)
THEN
RESPONSE #100
SetGlobal("O#TiaxExperienceSet","GLOBAL",1)
AddXPObject("O#Tiax",1600000)
END

// Hotkey fixer
IF
HotKey(K)
THEN
RESPONSE #100
StartCutSceneMode()
StartCutScene("O#TiaxF")
END

// Leaving AR1516
IF
InParty(Myself)
!AreaCheck("AR1516")
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Global("O#TiaxThankTalk","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("O#TiaxThankTalk","GLOBAL",1)
END

IF
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Global("O#TiaxThankTalk","GLOBAL",1)
THEN
RESPONSE #100
Interact(Player1)
END

// First friend talk
IF 
InParty(Myself)
Global("O#TiaxShutUp","GLOBAL",0)
Global("O#TiaxFriendTalk","GLOBAL",0)
THEN
RESPONSE #100
RealSetGlobalTimer("O#TiaxFriendTimer","GLOBAL",2000)
SetGlobal("O#TiaxFriendTalk","GLOBAL",1)
END

// Friend talks, sequential
IF
InParty(Myself)
RealGlobalTimerExpired("O#TiaxFriendTimer","GLOBAL")
Global("O#TiaxShutUp","GLOBAL",0)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
OR(5)
Global("O#TiaxFriendTalk","GLOBAL",1)
Global("O#TiaxFriendTalk","GLOBAL",3)
Global("O#TiaxFriendTalk","GLOBAL",5)
Global("O#TiaxFriendTalk","GLOBAL",7)
Global("O#TiaxFriendTalk","GLOBAL",9)
THEN
RESPONSE #100
IncrementGlobal("O#TiaxFriendTalk","GLOBAL",1)
SetGlobal("O#TiaxCheckF","GLOBAL",1)
Interact(Player1)
END

IF
InParty(Myself)
Global("O#TiaxCheckF","GLOBAL",1)
Global("O#TiaxShutUp","GLOBAL",0)
!AreaType(DUNGEON)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
OR(5)
Global("O#TiaxFriendTalk","GLOBAL",2)
Global("O#TiaxFriendTalk","GLOBAL",4)
Global("O#TiaxFriendTalk","GLOBAL",6)
Global("O#TiaxFriendTalk","GLOBAL",8)
Global("O#TiaxFriendTalk","GLOBAL",10)
THEN
RESPONSE #100
Interact(Player1)
END